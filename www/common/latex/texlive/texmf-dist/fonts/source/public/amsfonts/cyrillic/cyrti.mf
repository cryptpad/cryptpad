%% filename: cyrti.mf
%% version: 2.2
%% date: 1995/01/04
%%
%% American Mathematical Society
%% Technical Support
%% Publications Technical Group
%% 201 Charles Street
%% Providence, RI 02904
%% USA
%% tel: (401) 455-4080
%%      (800) 321-4267 (USA and Canada only)
%% fax: (401) 331-3842
%% email: tech-support@ams.org
%%
%% Copyright 1995 Humanities and Arts Computing Center, University of
%% Washington.
%%
%% Unlimited copying and redistribution of this file are permitted as
%% long as this file is not modified.  Modifications, and distribution
%% of modified versions, are permitted, but only if the resulting file
%% is renamed.
%% 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%cyrti.mf

cmchar "Italic Serbian letter Soft n (looks like hb)";
beginchar(oct"010",6u#,x_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(5u#,0); pickup tiny.nib; w:=6u;
pos1(stem,0); pos2(stem,0); x1=x2;
lft x1l=hround(u-.5stem); top y1=h; y2=.6bar_height;
filldraw stroke z1e--z2e;  % stem
sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % serif
pickup fine.nib; pos2'(stem,-180); pos3(vair,-90);
pos4(curve,0); pos5(vair,90); pos0(hair,180);
z0=z2=z2'; x3=x5=.5[x2,x4]; rt x4r=hround(w-u+.5curve);
bot y3r=-oo; top y5r=1.2bar_height+oo; y4=y2;
filldraw stroke pulled_arc.e(2',3) & pulled_arc.e(3,4)
 & pulled_arc.e(4,5) & super_arc.e(5,0);  % bowl
x80=-5u; x82-.5stem=hround(-2u-.5stem); hook_in(80,81,82); 
z83=z82; x84=x83; y84-.5stem=-oo;
pos83(stem,0); pos84(stem,0); filldraw circ_stroke z84e--z83e;
x85=x83; y85=y86=bar_height; x86=x0; pos85(hair,90); pos86(hair,90);
filldraw stroke z85e--z86e;
%y11=y1; x11=x1-4.5u; x12=x11; pos11(stem,0); x14=-.5u;
%hook_out(12,13,14);
%sloped_serif.l(11,12,b,1/3,jut,serif_drop);  % serif
%filldraw stroke z11e--z12e;
%x89=x11; y89=.75[y2,y3]; x90=x0; y90=y89; pos89(hair,90); pos90(hair,90);
%filldraw stroke z89e--z90e;
endchar;

cmchar "Italic Serbian letter soft l";
beginchar(oct"011",12u#,x_height#,0);
adjust_fit(.5u#,.5u#); pickup fine.nib; w:=9u;
y1+.375stem=h; x1=.5[x3,x1']; x2=x3=3u; y3=.35h; y2=.75h;
bot y4r=-oo; y1'=.8h; x1'l=w-2u;
x4=.5[x6,x3]; y6-.5flare=.1h; pos6(flare,-180);
pos5(hair,-180); lft x6r=-.25u; z5r=z6r;
pos4(vair,-90); pos2(hair,0); pos3(hair,0); pos1(.75stem,-90); 
x7=x1'; pos1'(stem,180); pos7(stem,0); 
z11=z1'; pos11(stem,0); y7=.6bar_height; 
bulb(4,5,6); 
filldraw stroke pulled_arc.e(4,3) &  super_arc.e(3,1) & super_arc.e(1,1');
filldraw stroke z11e..z7e; w:=12u;
x23=x25=.5[x7,x24]; rt x24r=hround(w-u+.5curve);
bot y23r=-oo; top y25r=1.2bar_height+oo; y24=y7; z7'=z7; pos7'(stem,-180);
z20=z7; pos20(hair,180); pos23(vair,-90); pos24(curve,0); pos25(vair,90);
filldraw stroke pulled_arc.e(7',23) & pulled_arc.e(23,24)
 & pulled_arc.e(24,25) & super_arc.e(25,20);  % bowl
endchar;

cmchar "Italic Serbian letter hard dj";
beginchar(oct"012",9.5u#,x_height#,desc_depth#);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); pickup fine.nib; interim superness:=more_super;
x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; x9=w; hook_out(7,8,9);  % closing hook
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
%
%x31=x7; y31=-.25d; x32=x3; y32=-.8d; x33=.25[x32,x31]; y33=-d;
%pos31(vair,180); pos32(slab,270); pos33(hair,360);
%filldraw stroke z4e{2,-1}...{down}z31e{down}...{down}z32e...{right}z33e;
x31=x7; y31=-.25d; x32=x3; y32=-.8d; x33=x32-1.5u; y33=y32+u;
pos31(vair,-180); pos32(.7stem,-270); pos33(hair,-360);
filldraw stroke z4e{2,-1}...z31e...{left}z32e...z33e;
%
math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#);
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

cmchar "Italic Russian letter reverse e (looks like backwards c)";
beginchar(oct"013",8u#,x_height#,0);
italcorr if math_fitting:1/3x_height#*slant else:x_height#*slant-u# fi;
adjust_fit(0,0); pickup fine.nib;
pos0(flare,0); pos1(hair,0); pos2(vair,90);
pos3(curve,180); pos4(vair,270); pos5(hair,320);
x2=x4=.5(w+u); rt x1r=max(rt x2,hround(w-.25u))+eps;
lft x3r=hround(1.5u-.5curve); x5r=good.x(w-eps); x6=x5;
y1=.5[bar_height,h]; top y2r=h+oo; bulb(2,1,0);  % bulb
bot y4r=-oo; y3=.5[y2,y4]; top y5l=vround .5bar_height; y6=bar_height;
path p; p=z4{right}..z5..z6;
filldraw stroke pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
x77=x4; y77=.5h; x78=2x77-x3; y78=2y77-y3;
flip(77); 
y79=y78; x79=x78-(x1-x3);
pos78(hair,90); pos79(hair,90); 
filldraw stroke z78e--z79e; 
endchar;

cmchar "Italic Ukrainian letter i";
beginchar(oct"014",5u#,min(asc_height#,10/7x_height#+.5flare#),0);
italcorr max(1/3x_height#*slant+.5hair#+.5u#,h#*slant+.5flare#-2u#);
adjust_fit(if monospace:u#,u# else: 0,0 fi); pickup fine.nib;
x0=0; x5=w; x2=.5w+.25u; x3=.5w-.25u;
hook_in(0,1,2)(skewed); hook_out(3,4,5)(skewed);  % hooks
filldraw stroke z2e--z3e;  % stem
pos8(flare,0); pos9(flare,90);
x8=.5w if not monospace:-.4(h-x_height)*slant fi; top y9r=h+1;
if bot y9l-top y1<slab: y9l:=min(y9r-eps,y1+fine+slab); fi
x8=x9; y8=.5[y9l,y9r]; dot(8,9);  % dot
math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#);
penlabels(0,1,2,3,4,5,8,9); endchar;

cmchar "Italic Ukrainian letter ye (looks like c with mid-line)";
beginchar(oct"015",8u#,x_height#,0);
italcorr if math_fitting:1/3x_height#*slant else:x_height#*slant-u# fi;
adjust_fit(0,0); pickup fine.nib;
pos0(flare,0); pos1(hair,0); pos2(vair,90);
pos3(curve,180); pos4(vair,270); pos5(hair,320);
x2=x4=.5(w+u); rt x1r=max(rt x2,hround(w-.25u))+eps;
lft x3r=hround(1.5u-.5curve); x5r=good.x(w-eps); x6=x5;
y1=.5[bar_height,h]; top y2r=h+oo; bulb(2,1,0);  % bulb
bot y4r=-oo; y3=.5[y2,y4]; top y5l=vround .5bar_height; y6=bar_height;
path p; p=z4{right}..z5..z6;
filldraw stroke pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
x78=x3; y78=y3; y79=y78; x79=x1;
pos78(hair,90); pos79(hair,90); 
filldraw stroke z78e--z79e; 
endchar;

cmchar "Italic Serbian letter dj (looks like barred h with tail)";
beginchar(oct"016",8u#,asc_height#,desc_depth#);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(1.5u#,0); pickup tiny.nib;
pos1(stem,0); pos2(stem,0); x1=x2;
lft x1l=hround(1.5u-.5stem); top y1=h; y2-.5stem=-oo;
filldraw circ_stroke z2e--z1e;  % left stem
sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % serif
pickup fine.nib; x4+.5stem=hround(w-.75u+.5stem);
x5=x4; %x5=x4-.25u; 
ital_arch(2,3,4);  % arch
x7=.45[x2,x4]; bot y7r=-d; pos7(hair,-90);
filldraw stroke pulled_arc.e(4,7);
x98=-u; x99=x4; y98=y99=.5x_height+.5h; pos98(bar,90); pos99(bar,90);
filldraw stroke z98e--z99e;
%x7=w; hook_out(5,6,7)(skewed);  % closing hook
%filldraw stroke z4e{down}..{-u,-x_height}z5e;  % right stem
math_fit(0,ic#); penlabels(1,2,3,4,5,6,7); endchar;

cmchar "Italic Serbian letter soft t (looks like barred h)";
beginchar(oct"017",9u#,asc_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(1.5u#,0); pickup tiny.nib;
pos1(stem,0); pos2(stem,0); x1=x2;
lft x1l=hround(1.5u-.5stem); top y1=h; y2-.5stem=-oo;
filldraw circ_stroke z2e--z1e;  % left stem
sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % serif
pickup fine.nib; x4+.5stem=hround(w-2.5u+.5stem);
x5=x4-.25u; ital_arch(2,3,4);  % arch
x7=w; hook_out(5,6,7)(skewed);  % closing hook
filldraw stroke z4e{down}..{-u,-x_height}z5e;  % right stem
x98=-u; x99=x4; y98=y99=.5x_height+.5h; pos98(bar,90); pos99(bar,90);
filldraw stroke z98e--z99e;
math_fit(0,ic#); penlabels(1,2,3,4,5,6,7); endchar;

cmchar "Italic Russian letter yu (looks like io)";
beginchar(oct"030",12u#,x_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,0); pickup fine.nib;
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5[x2,x4]; lft x2r=hround(5.5u-.5curve); x4=w-hround(1.5u-.5curve);
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1); % bowl
x0=0; x22-.5stem=hround(3u-.5stem); hook_in(0,21,22); 
z23=z22; x24=x23; y24-.5stem=-oo;
pos23(stem,0); pos24(stem,0); filldraw circ_stroke z24e--z23e;
x25=x23; y25=y26=bar_height; x26=x2; pos25(hair,90); pos26(hair,90);
filldraw stroke z25e--z26e;
penlabels(0,1,2,3,4,21,23,24,25,26); endchar;

cmchar "Italic Russian letter zhe";
beginchar(oct"031",20u#,x_height#,0);
italcorr if math_fitting:1/3x_height#*slant else:x_height#*slant-u# fi;
adjust_fit(0,0); pickup fine.nib;
w:=8u;
pos0(flare,0); pos1(hair,0); pos2(vair,90);
pos3(curve,180); pos4(vair,270); pos5(hair,320);
x2=x4=.5(w+u); rt x1r=max(rt x2,hround(w-u))+eps;
lft x3r=hround(1.5u-.5curve); x5r=good.x(w-eps); x6=x5;
y1=.5[bar_height,h]; top y2r=h+oo; bulb(2,1,0);  % bulb
bot y4r=-oo; y3=.5[y2,y4]; top y5l=vround .5bar_height; y6=bar_height;
path p; p=z4{right}..z5..z6;
filldraw stroke pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
path mypath; mypath= pulled_arc.r(2,3) & pulled_arc.r(3,4);
x200=0; y200=.75x_height; x201=18u; y201=y200;
path newpath; newpath= z200--z201;
z178r= newpath intersectionpoint mypath;
x77=x4; y77=.5h; z78r=z178r rotatedaround(z77,180);
x78r:=x78r-hair;
flip(77); w:=20u;
pos50(flare,0); pos51(hair,0); pos52(vair,90);
pos53(curve,180); pos54(vair,270); pos55(hair,320);
x52=x54=w-3.5u; rt x51r=max(rt x52,hround(w-u))+eps;
lft x53r=hround(w-6.5u-.5curve); x55r=good.x(w-eps); x56=x55;
y51=.5[bar_height,h]; top y52r=h+oo; bulb(52,51,50);  % bulb
bot y54r=-oo; y53=.5[y52,y54]; top y55l=vround .5bar_height; y56=bar_height;
path p; p=z54{right}..z55..z56;
filldraw stroke pulled_arc.e(52,53)
 & pulled_arc.e(53,54)...{direction 1 of p}z55e;  % arc
path mynewpath; mynewpath= pulled_arc.r(52,53) & pulled_arc.r(53,54);
x80=x81=.5[x78,x53]; y80=h-.5stem; y81=0; 
z79l=mynewpath intersectionpoint newpath;
pos78(hair,0); pos79(hair,0); pos80(stem,0); pos81(stem,0); 
filldraw stroke z78e--z80e; filldraw circ_stroke z80e--z81e; filldraw stroke z79e--z81e;
endchar;

cmchar "Italic Russian letter short i (looks like breve u)";
beginchar(oct"032",9.5u#,asc_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); pickup fine.nib; interim superness:=more_super; h:=x_height;
x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; x9=w; hook_out(7,8,9);  % closing hook
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
h:=asc_height;
pickup crisp.nib; pos38_1(vair,-180); pos38_3(vair,0);
top y38_1=top y38_3=y38_2r+cyrspread; lft x38_1r+.5u=lft x3r; rt x38_3r-.5u=rt x6r;
numeric mid_thickness; mid_thickness=vround 1/3[vair,stem];
pos38_2(mid_thickness,-90); x38_2=x4;
bot y38_2r=x_height+.5dot_size;
filldraw stroke z38_1e{down}...z38_2e{right}...{up}z38_3e;  % stroke
%% ngb changes:
if square_dots: % relax
else:
  pos38_11(dot_size,0); pos38_12(dot_size,90); pos38_13(dot_size,0); pos38_14(dot_size,90);
  lft x38_11l=lft x38_1r; rt x38_13r=rt x38_3r; y38_11=y38_12=y38_13=y38_14=y38_2r+cyrspread;
  x38_12=x38_11; x38_14=x38_13; dot(38_11,38_12); 
  if altaccent: % relax
  else: dot(38_13,38_14); fi
fi
math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#);
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

cmchar "Italic Russian letter yo (looks like e umlaut)";
beginchar(oct"033",8u#,asc_height#,0);
italcorr max(1/3x_height#*slant,x_height#*slant+.5(.2[hair#,stem#])-u#);
adjust_fit(0,0); pickup fine.nib; h:=x_height;
numeric heavy_hair; heavy_hair=hround .2[hair,stem];
pos0(vair,-90); pos1(heavy_hair,0); pos2(vair,90);
pos3(curve,180); pos4(vair,270); pos5(hair,320);
x0=rt x3l; rt x1r=hround(w-1.5u+.5heavy_hair); x2=x4=.5(w+u);
lft x3r=hround(1.5u-.5curve); x5r=good.x(w-eps); x6=x5;
y0=y3=y6=bar_height; y1=.5[y0,y2]; top y2r=h+oo; bot y4r=-oo;
top y5l=vround .5bar_height; path p; p=z4{right}..z5..z6;
filldraw stroke z0e{right}...z1e{up}...pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
umlaut(2,q);
math_fit(-.3x_height#*slant+.5curve#-u#,ic#);
penlabels(0,1,2,3,4,5,6); endchar;

%cmchar "Italic letter v";
%beginchar(oct"034",9.5u#,x_height#,0);
%italcorr x_height#*slant;
%adjust_fit(0,0); pickup fine.nib; interim superness:=more_super;
%x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
%hook_in(0,1,2)(skewed);  % opening hook
%pos2'(stem,-180); z2'=z2; pos4(vair,-90); pos5(hair,0);
%x4=5u; rt x5r=hround(w-2u); y3=.7[y4,y5]; bot y4r=-oo; y5=.57h;
%filldraw stroke z2'e{-u,-x_height}...super_arc.e(3,4)
% ..tension atleast 1.05..{up}z5e; % left stem and arc
%w:=10u; v_bulb(5,6);  % closing bulb
%math_fit(-2/3x_height#*slant+.5hair#+.5u#,2/3ic#);
%penlabels(0,1,2,3,4,5,6); endchar;

cmchar "Italic Old Russian letter izhitsa (looks like v)";
beginchar(oct"034",9u#,x_height#,0);
italcorr x_height#*slant+.25u#;
adjust_fit(serif_fit#,serif_fit#);
numeric left_stem,right_stem,outer_jut,alpha;
left_stem=stem-stem_corr;
right_stem=min(hair if hefty:-2stem_corr fi,left_stem);
outer_jut=.75jut; x1l=w-x4r-1.125u=l+letter_fit+outer_jut+.25u; y1=y4+.1h=h;
x2-x1=x4-x3; x2l+apex_corr=x3l; y2=y3=-apex_o;
alpha=diag_ratio(2,right_stem,y1-y2,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
x5=x4+1.125u; top y5r=h+oo; penpos5(hair,90); 
rt x6r=hround(w-.5u-.5); y6+.5flare=.95h; penpos6(hair,0);
penpos7(flare,0); if serifs: bulb(5,6,7); fi
if y0>notch_cut: y0:=notch_cut;
  fill z0+.5right{up}...{z4-z3}z4l...{right}z5r--z5l{left}
    ...z4r{z3-z4}...z3r...z3l--diag_end(2l,1l,1,1,1r,2r){z2-z1}
    ...{down}z0+.5left--cycle; % left and right diagonals
else: fill z0--z4l{z4-z3}...{right}z5r--z5l{left}...{z3-z4}z4r--z3r{-10,-1}...{-10,1}z3l
    --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/3,.3outer_jut,b,1/2,.3inner_jut);  % left serif
fi % dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark); fi  % right serif
math_fit(.75u#-height#*slant,ic#-4u#); penlabels(0,1,2,3,4); endchar;

cmchar "Italic Old Russian letter fita (looks like theta)";
beginchar(oct"035",10u#,x_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(u#,0); pickup fine.nib; w:=9u;
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(2.5u-.5curve); x4=w-x2;
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1); % bowl
w:=10u; x31=0; x32=w-hround u; y31=y32=y2;
pos31(bar,90); pos32(bar,90); filldraw circ_stroke z31e--z32e;
math_fit(-.3x_height#*slant+.5curve#-u#,ic#); penlabels(1,2,3,4); endchar;

cmchar "Italic Macedonian letter zelo (looks like s)";
beginchar(oct"036",5.25u#+max(1.75u#,flare#),x_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(0,0); pickup fine.nib;
numeric theta; theta=90-angle(40u,h); slope:=-h/40u;  % angle at middle
pos2(vair,-90); pos0(max(fine.breadth,ess),theta); pos7(vair,-90);
x2l=x0=x7=.5w; top y2l=h+oo; bot y7r=-oo;
y0-.5ess=y7l+.55(y2r-y7l-ess);
lft x3l=hround u-eps; rt x6r=hround(w-.5u)+eps;
x3r-x3l=x6r-x6l=hround .5[vair,ess]-fine;
ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
interim superness:=more_super;
filldraw stroke super_arc.e(2,3) & z3e{down}
 ..z4e---z5e..z6e{down} & super_arc.e(6,7);  % main stroke
pos1(hair,0); pos10(hround .75[hair,flare],0);
pos2'(vair,90); z2'=z2;
pos8(hair,-180); pos9(flare,-180);
rt x10r=hround(w-u)+2eps; lft x9r=hround .5u-2eps; y10=.78h; y9=.25h;
bulb(2',1,10); bulb(7,8,9);  % bulbs
math_fit(0,ic#); penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

cmchar "Italic Russian letter ya (looks like small backward R)";
beginchar(oct"037",9u#,x_height#,0);
italcorr x_height#*slant;
adjust_fit(0,0); pickup fine.nib; slantswitch;
x0=.25u; top y0r=h; x2-.5stem=hround(2.5u-.5stem); %hook_in(0,1,2);
y2=y0; pos2(stem,0); x11=x2; pos0(vair,90);
y11-.5stem=-oo; pos11(stem,0);
filldraw circ_stroke z11e--z2e;  % left stem
pos3(vair,90); x3=x11; y3=bar_height;
x4=x2; rt x16r=hround(w-1.5u+.4curve); x17=.5[x4,x16]; z15=z3;
%x15=x17; bot y15r=bar_height; 
top y17r=h; y4=y16=.5[y15,y17];
pos4(hair,-180); pos15(vair,-90); pos16(curve,0); pos17(vair,90);
filldraw circ_stroke z0e--z17e;
%filldraw stroke super_arc.e(4,15) & pulled_arc.e(15,16)
filldraw stroke pulled_arc.e(15,16) & pulled_arc.e(16,17) & super_arc.e(17,4);
pos7(stem,0); pos8(vair,90); pos9(hair,180);
x9+.5hair=hround(w+.5hair-eps); y9=1/3x_height;
lft x7l=hround(w-2.75u-.5stem); y7=1/2y3; x8=w-1.2u; bot y8l=-oo;
filldraw stroke z3e{right}...z7e{down}
 ...z8e{right}...{up}z9e;  % lower diagonal
mirror(.5w);
math_fit(0,1/3x_height#*slant+.5hair#+.5u#);
penlabels(1,2,3,4,5,6,7,8,9); endchar;
% this one /w hook-in is pretty ugly
%cmchar "Italic letter ya";
%beginchar(oct"037",9u#,x_height#,0);
%italcorr x_height#*slant;
%adjust_fit(0,0); pickup fine.nib; slantswitch;
%x0=.25u; top y0r=h; x2-.5stem=hround(2.5u-.5stem); %hook_in(0,1,2);
%y2=y0; pos2(stem,0); x11=x2;
%y11-.5stem=-oo; pos11(stem,0);
%filldraw circ_stroke z11e--z2e;  % left stem
%filldraw circ_stroke z0e--z17e;
%pos3(vair,90); x3=x11; y3=bar_height;
%x4=x2; rt x16r=hround(w-1.5u+.4curve); x17=.5[x4,x16]; z15=z3;
%%x15=x17; bot y15r=bar_height; 
%top y17r=h; y4=y16=.5[y15,y17];
%pos4(hair,-180); pos15(vair,-90); pos16(curve,0); pos17(vair,90);
%%filldraw stroke super_arc.e(4,15) & pulled_arc.e(15,16)
%filldraw stroke pulled_arc.e(15,16) & pulled_arc.e(16,17) & super_arc.e(17,4);
%pos7(stem,0); pos8(vair,90); pos9(hair,180);
%x9+.5hair=hround(w+.5hair-eps); y9=1/3x_height;
%lft x7l=hround(w-2.75u-.5stem); y7=1/2y3; x8=w-1.2u; bot y8l=-oo;
%filldraw stroke z3e{right}...z7e{down}
% ...z8e{right}...{up}z9e;  % lower diagonal
%mirror(.5w);
%math_fit(0,1/3x_height#*slant+.5hair#+.5u#);
%penlabels(1,2,3,4,5,6,7,8,9); endchar;

boolean modernyat; modernyat=false;
% the modern yat is a modified slanted version of yat in upright font
% the real italic yat is completely different
% I wouldn't even know the real one existed were it not for Dimitri Vulis
iff modernyat:
  cmchar "Italic Old Russian letter yat";
  beginchar(oct"053",8u#,asc_height#,0);
  italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
  adjust_fit(1.75u#,0); pickup tiny.nib;
  pos1(stem,0); pos2(stem,0); x1=x2;
  lft x1l=hround(1.5u-.5stem); top y1=h; y2=.5x_height;
  filldraw stroke z1e--z2e;  % stem
  sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % serif
  pickup fine.nib; pos2'(stem,-180); pos3(vair,-90);
  pos4(curve,0); pos5(vair,90); pos0(hair,180);
  z0=z2=z2'; x3=x5=.5[x2,x4]; rt x4r=hround(w-1.5u+.5curve);
  bot y3r=-oo; top y5r=x_height+oo; y4=y2;
  filldraw stroke pulled_arc.e(2',3) & pulled_arc.e(3,4)
   & pulled_arc.e(4,5) & super_arc.e(5,0);  % bowl
  x31=-1.25u; x32=2x1-x31; y31=y32=.5[y5,y1];
  pos31(bar,90); pos32(bar,90); filldraw stroke z31e--z32e;
  penlabels(0,1,2,3,4,5,31,32); endchar;
iff not modernyat:
  cmchar "Italic Old Russian letter yat";
  beginchar(oct"053",12u#,x_height#,0);
  italcorr 1/3x_height#*slant+.5hair#+.5u#;
  adjust_fit(0,0); pickup fine.nib; w:=10u;
  x0=0; x1=x2; pos2(stem,0); lft x2l=hround(2.5u-.5stem); y2-.5stem=-oo;
  hook_in(0,a,1);  % opening hook
  filldraw circ_stroke z2e--z1e;  % left stem
  x4+.5stem=hround(w-2.5u+.5stem); x5=x4; ital_arch(2,3,4);  % arch
  y5=.5bar_height; pos5(stem,0);
  filldraw stroke z4e{down}..z5e;  % right stem
  w:=12u;
  x23=x25=.5[x5,x24]; rt x24r=hround(w-u+.5curve);
  bot y23r=-oo; top y25r=1.2bar_height+oo; y24=y5; z7'=z5; pos7'(stem,-180);
  z20=z5; pos20(hair,180); pos23(vair,-90); pos24(curve,0); pos25(vair,90);
  filldraw stroke pulled_arc.e(7',23) & pulled_arc.e(23,24)
   & pulled_arc.e(24,25) & super_arc.e(25,20);  % bowl
  math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#);
  endchar;

cmchar "Italic letter dotless i";
beginchar("=",5u#,min(asc_height#,10/7x_height#+.5flare#),0);
italcorr max(1/3x_height#*slant+.5hair#+.5u#,h#*slant+.5flare#-2u#);
adjust_fit(if monospace:u#,u# else: 0,0 fi); pickup fine.nib;
x0=0; x5=w; x2=.5w+.25u; x3=.5w-.25u;
hook_in(0,1,2)(skewed); hook_out(3,4,5)(skewed);  % hooks
filldraw stroke z2e--z3e;  % stem
pos8(flare,0); pos9(flare,90);
x8=.5w if not monospace:-.4(h-x_height)*slant fi; top y9r=h+1;
if bot y9l-top y1<slab: y9l:=min(y9r-eps,y1+fine+slab); fi
x8=x9; y8=.5[y9l,y9r]; % dot(8,9);  % no dot
math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#);
penlabels(0,1,2,3,4,5,8,9); endchar;

cmchar "Italic Russian letter a";
beginchar("a",9u#,x_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); pickup fine.nib;
pos0(hair,0); pos1(vair,90); pos2(curve,180); pos3(vair,270);
pos4(stem,0); x4=x0; y4+.5stem=vround .98h;
x1=x3=.5[x0,x2]; lft x2r=hround(1.5u-.5curve);
rt x4r=hround(w-2.5u+.5stem);
top y1r=h+oo; bot y3r=-oo; y0=y2=.5[y1,y3];
filldraw stroke super_arc.e(0,1) & pulled_arc.e(1,2)
 & pulled_arc.e(2,3) & super_arc.e(3,0);  % bowl
x5=x4; x7=w; hook_out(5,6,7);  % closing hook
filldraw circ_stroke z4e--z5e;  % stem
math_fit(-.3x_height#*slant+.5curve#-u#,ic#);
penlabels(0,1,2,3,4,5,6,7); endchar;

cmchar "Italic Russian letter be";
beginchar("b",8u#,asc_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,0); pickup tiny.nib;
pos50(stem,-90); x50=.5w; y50=.95h; x100=w-u; y100=h; pos100(hair,0);
pos1(hair,0); pos2(stem,0); x1=x2+.5u; pos52(hair,0);
lft x1l=hround(1.5u-.5stem); top y1=1.2x_height; y2=.5x_height;
x52l=x2l; y52=y2;
filldraw stroke z100e...{left}z50e & pulled_arc.e(50,1) & z1e...{down}z52e;  % stem
pickup fine.nib; pos2'(stem,-180); pos3(vair,-90);
pos4(curve,0); pos5(vair,90); pos0(hair,180);
z0=z2=z2'; x3=x5=.5[x2,x4]; rt x4r=hround(w-1.5u+.5curve);
bot y3r=-oo; top y5r=x_height+oo; y4=y2;
z72=z2; pos72(stem,180);
filldraw stroke pulled_arc.e(2',3) & pulled_arc.e(3,4)
 & pulled_arc.e(4,5) & super_arc.e(5,72); %(5,0);  % bowl
math_fit(-.3x_height#*slant+.5curve#-u#,ic#);
penlabels(0,1,2,3,4,5); endchar;

cmchar "Italic Russian letter tse";
beginchar("c",9.5u#,x_height#,desc_depth#);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,.75u#); pickup fine.nib; interim superness:=more_super;
x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; %x9=w; hook_out(7,8,9);  % closing hook
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
pos7(stem,0);
x9=w; y9=y8=.4bar_height; x8=x6; pos8(hair,0); pos9(hair,180);
y7=y8;
filldraw circ_stroke z6e--z7e;  % right stem
filldraw circ_stroke z7e{down}...{up}z9e;
pickup crisp.nib;
y10=0; x10=x9-.1u; y12=-d; x12=x9-.75u; z11=.5[z10,z12]; 
pos10(hair,90); pos11(1.25stem,135);
filldraw stroke z9{down}...z10e...z11e--z12;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11); endchar;

cmchar "Italic Russian letter de";
beginchar("d",9u#,asc_height#,0);
italcorr .7asc_height#*slant+.5curve#-1.5u#;
adjust_fit(0,0);
numeric top_thickness,bot_thickness,side_thickness,pull;
top_thickness=max(fine.breadth,vround(slab-2vair_corr));
bot_thickness=max(fine.breadth,vround(slab-vair_corr));
side_thickness=max(fine.breadth,hround 1/4[curve,cap_curve]);
pull=min(.25,1.5superpull);
pickup fine.nib; pos2(top_thickness,90);
pos3(side_thickness,0); pos4(bot_thickness,-90);
pos5(side_thickness,-180); pos6(vair,-270);
lft x5r=w-rt x3r=hround max(.75u,1.5u-.5side_thickness);
x4=x6+.1u=x2+3.5u=.5w;
top y2r=h+o; y3=.5[y2,y4]; bot y4r=-o;
y5=.5[y4,y6]; top y6=vround 5/8h+o;
path p; p=pulled_super_arc.l(3,4)(pull);
numeric t; t=xpart(p intersectiontimes((x3r,y5)--(x4,y5)));
pos7(thin_join,0); z7l=point t of p;
(x,y6r)=whatever[z7l,z6l]; x6r:=max(x,.5[x5r,x6]);
filldraw stroke pulled_super_arc.e(2,3)(pull)
 & {{interim superness:=more_super; pulled_super_arc.e(3,4)(pull)}}
 & {{less_tense; pulled_super_arc.e(4,5)(pull) & pulled_super_arc.e(5,6)(pull)}}
 & z6e{right}...{direction t of p}z7e;  % arc and bowl
%pos1(hair,180); pos0(flare,180);
%lft x0r=min(lft x2-eps,hround 1.2u);
%y0=vround min(.85h-.5flare,.8h+.5flare)+o;
%{{less_tense; bulb(2,1,0)}};  % arc and bulb
math_fit(-.3x_height#*slant+.5curve#-u#,ic#-u#);
penlabels(0,1,2,3,4,5,6,7); endchar;

cmchar "Italic Russian letter e";
beginchar("e",8u#,x_height#,0);
italcorr max(1/3x_height#*slant,x_height#*slant+.5(.2[hair#,stem#])-u#);
adjust_fit(0,0); pickup fine.nib;
numeric heavy_hair; heavy_hair=hround .2[hair,stem];
pos0(vair,-90); pos1(heavy_hair,0); pos2(vair,90);
pos3(curve,180); pos4(vair,270); pos5(hair,320);
x0=rt x3l; rt x1r=hround(w-1.5u+.5heavy_hair); x2=x4=.5(w+u);
lft x3r=hround(1.5u-.5curve); x5r=good.x(w-eps); x6=x5;
y0=y3=y6=bar_height; y1=.5[y0,y2]; top y2r=h+oo; bot y4r=-oo;
top y5l=vround .5bar_height; path p; p=z4{right}..z5..z6;
filldraw stroke z0e{right}...z1e{up}...pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
math_fit(-.3x_height#*slant+.5curve#-u#,ic#);
penlabels(0,1,2,3,4,5,6); endchar;

cmchar "Italic Russian letter ef (looks like phi)";
beginchar("f",max(1.5u#,stem#)+max(3.5u#,2flare#),asc_height#,asc_height#-x_height#);
italcorr asc_height#*slant+.75u#;
adjust_fit(if monospace:3u#,3u# else: 2.5u#,2.5u# fi); pickup fine.nib;
numeric theta; z88=(.5w-.5u,h); z89=(.5w+.5u,-d); theta=angle(z88-z89);
pos0(flare,0); pos1(hair,0); pos2(vair,90);
pos3(.5[hair,stem],180); pos4(stem,theta+90);
pos5(stem,theta-90); pos6(.5[hair,stem],0);
pos7(vair,-90); pos8(hair,-180); pos9(flare,-180);
rt x1r=hround(w+.25u); lft x8r=hround-.35u; x2=.6[x1,x4]; x7=.6[x8,x5];
y9-.5flare=vround -.85d; y2-y0=y9-y7; top y2r=h+oo; bot y7r=-d-oo;
y4=.25[x_height,h]; y5=.5[-d,y4];
z4=whatever[z88,z89]; z5=whatever[z88,z89];
x3=.8[x2+x4-x88,x88]; x6=.8[x7+x5-x89,x89]; y3=.8[y4,y2]; y6=.8[y5,y7];
bulb(2,1,0); bulb(7,8,9);  % bulbs
filldraw stroke z2e{left}...z3e...{z89-z88}z4e;  % upper arc
filldraw z4r--z5l--z5r--z4l--cycle;  % stem
filldraw stroke z5e{z89-z88}...z6e...{left}z7e;  % lower arc
pickup crisp.nib; pos20(bar,90); pos21(bar,90);
top y20r=top y21r=x_height;
lft x20=lft x4r-.5stem-u; rt x21=rt x4l+.5stem+1.5u;
%filldraw stroke z20e--z21e;  %  crossbar
x100=x5; y100=y102=.5[y101,y103]; top y101r=x_height+oo; bot y103r=-oo;
x101=x103=.5[x102,x100]; x102=x100-4u; pos100(hair,0); pos101(vair,90);
pos102(curve,180); pos103(vair,270);
filldraw stroke super_arc.e(100,101) & pulled_arc.e(101,102) 
 & pulled_arc.e(102,103) & super_arc.e(103,100);
y201=y101; y202=y102; y203=y103; x201-x100=x100-x101; x202-x100=x100-x102; 
x203-x100=x100-x103; pos201(vair,-90); pos202(curve,-180); pos203(vair,-270);
filldraw stroke super_arc.e(100,201) & pulled_arc.e(201,202) 
 & pulled_arc.e(202,203) & super_arc.e(203,100);
math_fit(desc_depth#*slant+u#,x_height#*slant);
penlabels(0,1,2,3,4,5,6,7,8,9,20,21,88,89); endchar;

cmchar "Italic Russian letter ghe";
beginchar("g",7u#,x_height#,0);
italcorr max(1/3x_height#*slant+.5hair#+.5u#,h#*slant+.5flare#-2u#);
adjust_fit(if monospace:u#,u# else: 0,0 fi); pickup fine.nib;
x0=0; x5=w; x2=.5w+1.5u; x3=.5w-1.5u;
hook_in(0,1,2)(skewed); hook_out(3,4,5)(skewed);  % hooks
filldraw stroke z2e{down}...{down}z3e;  % stem
penlabels(0,1,2,3,4,5,8,9); endchar;

cmchar "Italic Russian letter kha (looks like x)";
beginchar("h",6.5u#+max(1.5u#,flare#),x_height#,0);
italcorr max(1/3x_height#*slant+.5hair#+.5u#,x_height#*slant+.25u#);
adjust_fit(0,0); pickup fine.nib;
pos0(hair,180); pos1(vair,90); pos2(stem,0);
pos3(stem,-180); pos4(vair,-90); pos5(hair,0);
y0=y2=2/3h; y3=y5=1/3h; top y1r=h+oo; bot y4r=-oo;
rt x2r=hround(.5w+.5stem-eps); x2=x3;
x0-.5hair=hround-.5hair; x5+.5hair=hround(w+.5hair-eps);
x1=.5[x0,x2]; x4=.5[x3,x5];
filldraw stroke z0e{up}...pulled_arc.e(1,2); % opening hook
filldraw z2l--z3r--z3l--z2r--cycle;  % stem
filldraw stroke pulled_arc.e(3,4)...{up}z5e;  % closing hook
pos3'(hair,180); pos6(hair,180); pos7(vair,90);
pos8(hair,0); pos9(flare,0);
x3'l=x6l=x3l; y3'=y3; y6=3/4h; x7=.5[x8,x2]; top y7r=h+oo;
rt x8r=hround(w-.25u); y8+.5flare=vround(bot y7l-.03x_height);
filldraw stroke z3'e---z6e...{right}z7e; bulb(7,8,9);  % upper link and bulb
pos2'(hair,0); pos16(hair,0); pos17(vair,-90);
pos18(hair,-180); pos19(flare,-180);
x2'l=x16l=x2l; y2'=y2; y16=1/4h; x17=.5[x18,x3]; bot y17r=-oo;
lft x18r=hround.25u; y18-.5flare=vround(top y17l+.03x_height);
filldraw stroke z2'e---z16e...{left}z17e; bulb(17,18,19); % lower link and bulb
math_fit(0,ic#); penlabels(0,1,2,3,4,5,6,7,8,9,16,17,18,19); endchar;

cmchar "Italic Russian letter i (looks like u)";
beginchar("i",9.5u#,x_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); pickup fine.nib; interim superness:=more_super;
x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; x9=w; hook_out(7,8,9);  % closing hook
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#);
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

cmchar "Italic Serbian letter je";
beginchar("j",5u#,min(asc_height#,10/7x_height#+.5flare#),desc_depth#);
italcorr h#*slant+.5stem#-u#;
adjust_fit(if monospace:2u#,.5u# else: 0,0 fi); pickup fine.nib;
x0=0; x2=x3; pos3(stem,0); lft x3l=hround(w-1.5u-.5stem);
hook_in(0,1,2);  % opening hook
pos4(vair,-90); pos5(hair,-180); pos6(flare,-180);
bot y3=-1/3d; bot y4r=-d-oo; y6-.5flare=-vround.9d;
x4=1/3(w-u); lft x5r=min(hround-.5u,lft x5r+x4-x5l-eps);
filldraw stroke z2e---z3e...{left}z4e;  % stem and arc
bulb(4,5,6);  % bulb
pos8(flare,0); pos9(flare,90);
rt x8r=rt x2r if not monospace:-.6(h-x_height)*slant fi; top y9r=h+1;
if bot y9l-top y1<slab: y9l:=min(y9r-eps,y1+fine+slab); fi
x8=x9; y8=.5[y9l,y9r]; dot(8,9);  % dot
math_fit(desc_depth#*slant,x_height#*slant+.5stem#-u#);
penlabels(0,1,2,3,4,5,6,8,9); endchar;

cmchar "Italic Russian letter ka";
beginchar("k",9u#,x_height#,0);
italcorr x_height#*slant;
adjust_fit(0,0); pickup fine.nib;
x0=0; x2-.5stem=hround(2.5u-.5stem); hook_in(0,1,2);
z2'=z2;
pos2'(stem,0); x11=x2';
y11-.5stem=-oo; pos11(stem,0);
filldraw circ_stroke z11e--z2'e;  % left stem
pos3(vair,90); x3=x11; y3=bar_height;
pos4(vair,90); x4=w-2u; top y4r=x_height+oo;
filldraw stroke z3e{right}..{right}z4e;  % upper diagonal
pos5(hair,0); pos6(flare,0);
rt x5r=hround(w-.5u); y5+.5flare=vround(bot y4l-.03x_height);
bulb(4,5,6);  % bulb
pos7(stem,0); pos8(vair,90); pos9(hair,180);
x9+.5hair=hround(w+.5hair-eps); y9=1/3x_height;
lft x7l=hround(w-2.75u-.5stem); y7=1/2y3; x8=w-1.2u; bot y8l=-oo;
filldraw stroke z3e{right}...z7e{down}
 ...z8e{right}...{up}z9e;  % lower diagonal
math_fit(0,1/3x_height#*slant+.5hair#+.5u#);
penlabels(1,2,3,4,5,6,7,8,9); endchar;


cmchar "Italic Russian letter el";
beginchar("l",9u#,x_height#,0);
adjust_fit(.5u#,.5u#); pickup fine.nib;
y1+.375stem=h; x1=.5[x3,x1']; x2=x3=3u; y3=.35h; y2=.75h; bot y4r=-oo; y1'=.8h; x1'l=w-2u;
x4=.5[x6,x3]; y6-.5flare=.1h; pos6(flare,-180); pos5(hair,-180); lft x6r=-.25u; z5r=z6r;
pos4(vair,-90); pos2(hair,0); pos3(hair,0); pos1(.75stem,-90); 
x7=x1'; pos1'(stem,180); %pos7(stem,0); %hook_out provides pos7 statement
z11=z1'; pos11(stem,0);
bulb(4,5,6); 
filldraw stroke pulled_arc.e(4,3) &  super_arc.e(3,1) & super_arc.e(1,1');
%filldraw stroke pulled_arc.e(4,3) & z3e..z2e{up}...{right}z1e;
x9=w;
hook_out(7,8,9);
filldraw stroke z11e..z7e;
endchar;

cmchar "Italic Russian letter em";
beginchar("m",13u#,x_height#,0);
adjust_fit(.5u#,.5u#); pickup fine.nib;
x3=3u; y3=.35h; bot y4r=-oo; x7r=w-2u;
x4=.5[x6,x3]; y6-.5flare=.1h; pos6(flare,-180); pos5(hair,-180); lft x6r=-.25u; z5r=z6r;
pos4(vair,-90); pos3(hair,0);  
bulb(4,5,6); 
x9=w;
hook_out(7,8,9);
pos1'(hair,0); x1'l=x1l; y1'=y1=h;
x1l=x3; bot y10l=-oo; x10l=.48[x1,x7]; bot y11l=-oo; x11l=x10l; x13=x7; x12=x13l; y12r=y13=h;
pos1(stem,0); pos11(hair,90); pos12(hair,90); pos13(stem,0); pos10(stem,angle(z12-z11)); 
filldraw stroke pulled_arc.e(4,3) & z3e--z1'e;
filldraw stroke z1e--z10e; filldraw stroke z11e--z12e;
filldraw stroke z13e--z7e;
endchar;

cmchar "Italic Russian letter en (looks like h)";
beginchar("n",10u#,x_height#,desc_depth#);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,.25u#); pickup fine.nib; 
x0=0; x2=x3; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2);  % opening hook
y3-.5stem=-oo; z2'=z2; pos2'(stem,-180); filldraw circ_stroke z3e--z2'e;
x10=x3l; y10=y11=.55h; pos10(hair,90); pos11(hair,90);
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem); y6+.5stem=h;
x7=x6; x9=w; hook_out(7,8,9);  z7'=z7; pos7'(stem,0);
filldraw circ_stroke z6e--z7'e; x11=x6l; filldraw stroke z10e{1,1}...{1,1}z11e;
endchar;

cmchar "Italic Russian letter o";
beginchar("o",9u#,x_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,0); pickup fine.nib;
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5curve); x4=w-x2;
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1); % bowl
math_fit(-.3x_height#*slant+.5curve#-u#,ic#); penlabels(1,2,3,4); endchar;

cmchar "Italic Russian letter pe (looks like pi)";
beginchar("p",10u#,x_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); pickup fine.nib;
x0=0; x1=x2; pos2(stem,0); lft x2l=hround(2.5u-.5stem); y2-.5stem=-oo;
hook_in(0,a,1);  % opening hook
filldraw circ_stroke z2e--z1e;  % left stem
x4+.5stem=hround(w-2.5u+.5stem); x5=x4-.25u; ital_arch(2,3,4);  % arch
x7=w; hook_out(5,6,7)(skewed);  % closing hook
filldraw stroke z4e{down}..{-u,-x_height}z5e;  % right stem
math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#);
penlabels(0,a,1,2,3,4,5,6,7); endchar;

cmchar "Italic Russian letter che";
beginchar("q",9.5u#,x_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(0,0); pickup fine.nib; interim superness:=more_super;
x0=0; x2=x3; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-2.5u+.5stem);
x5=x6=x7; x9=w; hook_out(7,8,9);  % closing hook
y3=.7[y4,y5]; bot y4r=.75bar_height; y5=.75h; y6+.5stem=h;
filldraw stroke z2'e...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e--z7e;  % right stem
math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#);
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

cmchar "Italic Russian letter er (looks like p)";
beginchar("r",9u#,x_height#,desc_depth#);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,0); pickup fine.nib;
x0=0; x2-.5stem=hround(2.5u-.5stem); hook_in(0,1,2);  % opening hook
pos4(hair,-180); pos5(vair,-90); pos6(curve,0); pos7(vair,90);
x4=x2; rt x6r=hround(w-1.5u+.5curve); x5=x7=.5[x4,x6];
bot y5r=-oo; top y7r=h+oo; y4=y6=.5[y5,y7];
filldraw stroke super_arc.e(4,5) & pulled_arc.e(5,6)
 & pulled_arc.e(6,7) & super_arc.e(7,4);  % bowl
pickup tiny.nib; pos2'(stem,0); pos3(stem,0);
z2=z2'; x3=x2; bot y3=-d; filldraw stroke z2'e--z3e;  % stem
dish_serif(3,2',a,1/3,.75jut,b,1/3,jut);  % serif
math_fit(-min(2/3x_height#*slant-.5hair#-.5u#,
 2u#-.5stem#-desc_depth#*slant),ic#);
penlabels(0,1,2,3,4,5,6,7); endchar;

cmchar "Italic Russian letter es (looks like c)";
beginchar("s",8u#,x_height#,0);
italcorr if math_fitting:1/3x_height#*slant else:x_height#*slant-u# fi;
adjust_fit(0,0); pickup fine.nib;
pos0(flare,0); pos1(hair,0); pos2(vair,90);
pos3(curve,180); pos4(vair,270); pos5(hair,320);
x2=x4=.5(w+u); rt x1r=max(rt x2,hround(w-u))+eps;
lft x3r=hround(1.5u-.5curve); x5r=good.x(w-eps); x6=x5;
y1=.5[bar_height,h]; top y2r=h+oo; bulb(2,1,0);  % bulb
bot y4r=-oo; y3=.5[y2,y4]; top y5l=vround .5bar_height; y6=bar_height;
path p; p=z4{right}..z5..z6;
filldraw stroke pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
math_fit(-.3x_height#*slant+.5curve#-u#,ic#);
penlabels(0,1,2,3,4,5,6); endchar;

cmchar "Italic Russian letter te (looks like m)";
beginchar("t",15u#,x_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(if monospace:-1.5u#,-2u# else: 0,0 fi); pickup fine.nib;
numeric shaved_stem; shaved_stem=mfudged.stem;
save stem; stem=shaved_stem;
pos2(stem,0); x1=x2;
if monospace: pos1(stem,0); lft x1l=hround(2.5u-.5stem); top y1=h;
else: x0=0; lft x2l=hround(2.5u-.5stem);
 hook_in(0,a,1); fi  % opening hook
y2-.5stem=-oo; filldraw circ_stroke z2e--z1e;  % left stem
x4+.5stem=hround(.5w+.5stem); ital_arch(2,3,4);  % left arch
pos5(stem,0); y5=y2; x5=x4;
filldraw circ_stroke z5e--z4e;  % middle stem
x7+.5stem=hround(w-2.5u+.5stem);
x8=x7-.25u; ital_arch(5,6,7);  % right arch
if monospace: pos9(vair,90); x9=good.x .5[x8,w]; bot y9l=0; y8=1/3h;
 pos8(stem,0); filldraw stroke z8e{-u,-x_height}...{right}z9e; % terminal
else: x9=w; hook_out(8,b,9)(skewed); fi  % closing hook
filldraw stroke z7e{down}..{-u,-x_height}z8e;  % right stem
math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#);
penlabels(0,a,1,2,3,4,5,6,7,8,9); endchar;

cmchar "Italic Russian letter u (looks like y)";
beginchar("u",8.5u#,x_height#,desc_depth#);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0); pickup fine.nib; interim superness:=more_super;
x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0); x4=.5[x3,x5];
pos6(stem,0); rt x6r=hround(w-1.5u+.5stem);
pos7(stem,0); pos8(vair,-90);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h; y6+.5stem=h;
y7=0; bot y8r=-d-oo; x5=x6=x7; x8=.5w;
pos9(hair,-180); pos10(flare,-180); y9=-.5d;
lft x9r=hround(2.75u-.5flare); bulb(8,9,10);  % bulb
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e---z7e...{left}z8e;  % right stem and link
math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#-1/3x_height#*slant);
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

cmchar "Italic Russian letter ve (looks like B)";
beginchar("v",9u#,x_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,.5u#); pickup fine.nib;
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5curve); x4=w-x2; x5=x4+.5u;
top y1r=h+oo; y2=.5[y1,y3]-u; bot y3r=-oo; y4=.65h; y5=.28h; y7=y6; y6=y2+u;
x6=x4-2.5u; x7=x2l; pos5(curve,360); pos6(hair,90); z6'=z6; pos6'(hair,-90);
pos7(hair,90);
filldraw stroke pulled_arc.e(6',4) & pulled_arc.e(4,1) & pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,5) & pulled_arc.e(5,6); % bowl
filldraw stroke z7e--z6e;
penlabels(1,2,3,4,5,6); endchar;

cmchar "Italic Russian letter shcha";
beginchar("w",12u#,x_height#,desc_depth#);
italcorr x_height#*slant;
adjust_fit(if monospace:-1.5u#,-u# else: 0,2.5u# fi);
pickup fine.nib; interim superness:=more_super; begingroup
forsuffixes $=hair,stem: shaved$:=mfudged$; save $; $=shaved$; endfor
x0=0; x2=x3+.25u; pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
if monospace: pos1(vair,90); x1=good.x .5[x0,x2]; top y1r=h; y2=2/3h;
 pos2(stem,0); filldraw stroke z1e{right}...{-u,-x_height}z2e;
else: hook_in(0,1,2)(skewed); fi  % opening hook
pos2'(stem,-180); z2'=z2;
pos4(vair,-90); pos5(hair,0);
x4=.6[x2,x5]; x5=x6=x7; pos6(stem,-180); pos7(stem,-180);
y3=.7[y4,y5]; bot y4r=-oo; y5=.57h;
rt x6l=hround(rt x6l+.5w+.75u-x6); y6+.5stem=h; y7=y3;
pos8(vair,-90); pos9(hair,0);
x8=w-2.75u; rt x9r=hround(w-.5u); bot y8r=-oo; y9=y5;
filldraw stroke z2'e{-u,-x_height}
 ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw circ_stroke z6e..super_arc.e(7,8)...{up}z9e;  % middle stem and arc
%v_bulb(9,10);  % closing bulb
pos26(stem,0); x26=x27=x9; y26+.5stem=h; y27=y28=y29=.4bar_height;
pos27(stem,0); filldraw circ_stroke z26e--z27e;
x29=r-.5u; x28=x26; pos28(hair,0); pos29(hair,180);
filldraw circ_stroke z27e{down}...{up}z29e;
pickup crisp.nib;
%y30=0; x30=.5[x28,x29]; y32=-d; x32=x26-u; z31=.5[z30,z32]; 
y30=0; x30=x29-.1u; y32=-d; x32=x29-.75u; z31=.5[z30,z32]; 
pos30(hair,90); pos31(1.25stem,135);
filldraw stroke z29{down}...z30e...z31e--z32;
endgroup;
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

cmchar "Italic Russian letter sha";
beginchar("x",15u#,x_height#,0);
italcorr 1/3x_height#*slant+.5hair#+.5u#;
adjust_fit(if monospace:-1.5u#,-2u# else: 0,0 fi); pickup fine.nib;
numeric shaved_stem; shaved_stem=mfudged.stem;
save stem; stem=shaved_stem;
pos2(stem,0); x1=x2;
if monospace: pos1(stem,0); lft x1l=hround(2.5u-.5stem); top y1=h;
else: x0=0; lft x2l=hround(2.5u-.5stem);
 hook_in(0,a,1); fi  % opening hook
y2-.5stem=-oo; filldraw circ_stroke z2e--z1e;  % left stem
x4+.5stem=hround(.5w+.5stem); ital_arch(2,3,4);  % left arch
pos5(stem,0); y5=y2; x5=x4;
filldraw circ_stroke z5e--z4e;  % middle stem
x7+.5stem=hround(w-2.5u+.5stem);
x8=x7-.25u; ital_arch(5,6,7);  % right arch
if monospace: pos9(vair,90); x9=good.x .5[x8,w]; bot y9l=0; y8=1/3h;
 pos8(stem,0); filldraw stroke z8e{-u,-x_height}...{right}z9e; % terminal
else: x9=w; hook_out(8,b,9)(skewed); fi  % closing hook
filldraw stroke z7e{down}..{-u,-x_height}z8e;  % right stem
x77=.5w; y77=.5h; flip(77);
math_fit(-2/3x_height#*slant+.5hair#+.5u#,ic#);
endchar;

cmchar "Italic Russian letter ery";
beginchar("y",11u#,x_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(u#,0); pickup tiny.nib; w:=6u;
pos1(stem,0); pos2(stem,0); x1=x2;
lft x1l=hround(u-.5stem); top y1=h; y2=.6bar_height;
filldraw stroke z1e--z2e;  % stem
sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % serif
pickup fine.nib; pos2'(stem,-180); pos3(vair,-90);
pos4(curve,0); pos5(vair,90); pos0(hair,180);
z0=z2=z2'; x3=x5=.5[x2,x4]; rt x4r=hround(w-u+.5curve);
bot y3r=-oo; top y5r=1.2bar_height+oo; y4=y2;
filldraw stroke pulled_arc.e(2',3) & pulled_arc.e(3,4)
 & pulled_arc.e(4,5) & super_arc.e(5,0);  % bowl
w:=11u;
y11=y1; x11=x1+6.5u; x12=x11; pos11(stem,0); x14=w-.5u;
hook_out(12,13,14);
sloped_serif.l(11,12,b,1/3,jut,serif_drop);  % serif
filldraw stroke z11e--z12e;
math_fit(-.3x_height#*slant+.5curve#-u#,ic#);
penlabels(0,1,2,3,4,5); endchar;

cmchar "Italic Russian letter ze (looks like reversed epsilon)";
beginchar("z",7u#,x_height#,0);
italcorr if math_fitting:1/3x_height#*slant else:x_height#*slant-u# fi;
adjust_fit(0,0); pickup fine.nib; w:=8u;
pos0(flare,0); pos1(hair,0); pos2(vair,90);
pos33(curve,180); pos34(curve,180); pos4(vair,270); pos5(hair,320);
x2=x4=.5(w+u); rt x1r=max(rt x2,hround(w-u))+eps;
lft x33r=hround(1.5u-.5curve); x5r=good.x(w-u-eps); x6=x5; x34=x33;
y1=.5[bar_height,h]; top y2r=h+oo; bulb(2,1,0);  % bulb
bot y4r=-oo; y33=.25[y2,y4]; y34=.7[y2,y4]; top y5l=vround .5bar_height; y6=bar_height;
x3=x2; y3=.5[y2,y4]; pos3(hair,270); z3'=z3; pos3'(hair,90);
path p; p=z4{right}..z5..z6;
filldraw stroke pulled_arc.e(2,33) & pulled_arc.e(33,3);
filldraw stroke pulled_arc.e(3',34) & pulled_arc.e(34,4)...{direction 1 of p}z5e;  % arc
x77=x4-.5u; y77=.5h; flip(77);
math_fit(-.3x_height#*slant+.5curve#-u#,ic#);
endchar;

cmchar "Italic Russian letter soft sign";
beginchar(oct"176",6u#,x_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(u#,0); pickup tiny.nib; 
pos1(stem,0); pos2(stem,0); x1=x2;
lft x1l=hround(u-.5stem); top y1=h; y2=.6bar_height;
filldraw stroke z1e--z2e;  % stem
sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % serif
pickup fine.nib; pos2'(stem,-180); pos3(vair,-90);
pos4(curve,0); pos5(vair,90); pos0(hair,180);
z0=z2=z2'; x3=x5=.5[x2,x4]; rt x4r=hround(w-u+.5curve);
bot y3r=-oo; top y5r=1.2bar_height+oo; y4=y2;
filldraw stroke pulled_arc.e(2',3) & pulled_arc.e(3,4)
 & pulled_arc.e(4,5) & super_arc.e(5,0);  % bowl
math_fit(-.3x_height#*slant+.5curve#-u#,ic#);
endchar;

cmchar "Italic lowercase hard sign";
beginchar(oct"177",6u#,x_height#,0);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(u#,0); pickup tiny.nib; 
pos1(hair,0); pos1'(hair,-45); pos2(stem,0); rt x1r=hround(w-u);
x1'=x1-.25u; y1'=y1-.75u;
lft x2l=hround(u-.5stem); top y1=h; y2=.6bar_height;
filldraw stroke z2e{up}...{3,1}z1'e..z1e;  % stem
pos100(hair,180); pos101(.6[hair,curve],60); pos102(hair,90);
z102r=z1r; x100=lft x2l-.5u; y100=.675[y2,y1]; x101=.385[x100,x102];
top y101r=top y102r;
filldraw stroke z100e{up}...{5,-1}z101e...{1,1}z102e; % fillip
pickup fine.nib; pos2'(stem,-180); pos3(vair,-90);
pos4(curve,0); pos5(vair,90); pos0(hair,180);
z0=z2=z2'; x3=x5=.5[x2,x4]; rt x4r=hround(w-u+.5curve);
bot y3r=-oo; top y5r=1.2bar_height+oo; y4=y2;
filldraw stroke pulled_arc.e(2',3) & pulled_arc.e(3,4)
 & pulled_arc.e(4,5) & super_arc.e(5,0);  % bowl
endchar;

endinput;                    %%%%%%%%%%%%%%%%%%%%

cmchar "Italic Russian letter zhe (alternate to oct"031");
beginchar(oct"226",19u#,x_height#,0);
italcorr if math_fitting:1/3x_height#*slant else:x_height#*slant-u# fi;
adjust_fit(0,0); pickup fine.nib;
w:=8u;
pos0(flare,0); pos1(hair,0); pos2(vair,90);
pos3(curve,180); pos4(vair,270); pos5(hair,320);
x2=x4=.5(w+u); rt x1r=max(rt x2,hround(w-u))+eps;
lft x3r=hround(1.5u-.5curve); x5r=good.x(w-eps); x6=x5;
y1=.5[bar_height,h]; top y2r=h+oo; bulb(2,1,0);  % bulb
bot y4r=-oo; y3=.5[y2,y4]; top y5l=vround .5bar_height; y6=bar_height;
path p; p=z4{right}..z5..z6;
filldraw stroke pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{direction 1 of p}z5e;  % arc
x77=x4; y77=.5h; x78=2x77-x3; %y78=2y77-y3;
y78=bar_height;
flip(77); w:=19u;
pos50(flare,0); pos51(hair,0); pos52(vair,90);
pos53(curve,180); pos54(vair,270); pos55(hair,320);
x52=x54=w-3.5u; rt x51r=max(rt x52,hround(w-u))+eps;
lft x53r=hround(w-6.5u-.5curve); x55r=good.x(w-eps); x56=x55;
y51=.5[bar_height,h]; top y52r=h+oo; bulb(52,51,50);  % bulb
bot y54r=-oo; y53=.5[y52,y54]; top y55l=vround .5bar_height; y56=bar_height;
path p; p=z54{right}..z55..z56;
filldraw stroke pulled_arc.e(52,53)
 & pulled_arc.e(53,54)...{direction 1 of p}z55e;  % arc
x80=x81=.5[x78,x53]; y80=h; y81=0; y79=y78; x79=x53;
pos78(hair,90); pos79(hair,90); pos80(stem,0); pos81(stem,0);
filldraw stroke z78e--z79e; filldraw stroke z80e--z81e;
%addto currentpicture also V shifted(10u,0);
endchar;
